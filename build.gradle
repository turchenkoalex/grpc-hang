plugins {
    id 'java'
    id 'application'
    id 'com.google.protobuf' version '0.9.4'
}

// Provide the grpcVersion property for testing purposes
var grpcVersion = "$System.env.GRPC_VERSION"
if (grpcVersion == "null" || grpcVersion.isEmpty()) {
    // current version of grpc used by the project
    grpcVersion = '1.62.2'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

application {
    mainClass = 'com.example.Main'
}

run {
    systemProperties["java.util.logging.config.file"] = "logging.properties"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.1"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

dependencies {
    runtimeOnly "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}
